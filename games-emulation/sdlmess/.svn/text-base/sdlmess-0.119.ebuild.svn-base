# Copyright 1999-2007 Gentoo Foundation
# Distributed under the terms of the GNU General Public License v2
# $Header: $

inherit flag-o-matic toolchain-funcs eutils games

MY_PV=${PV/./}
MY_PV=${MY_PV/_p/u}
MY_P=${PN}${MY_PV}

DESCRIPTION="Multiple Emulator Super System for SDL"
HOMEPAGE="http://rbelmont.mameworld.info/?page_id=163"
SRC_URI="http://rbelmont.mameworld.info/${MY_P}.zip"

LICENSE="MAME"
SLOT="0"
KEYWORDS="~amd64 ~ppc ~ppc64 ~x86"
IUSE="custom-cflags debug dynarec tools"
# Needs fetch restriction due to $HOMEPAGE blocking wget downloads
RESTRICT="fetch"

RDEPEND="dev-libs/expat
	sys-libs/zlib
	>=media-libs/libsdl-1.2.11
	virtual/opengl
	virtual/glu
	x11-libs/libXext
	x11-libs/libXau
	x11-libs/libXdmcp
	debug?	( >=x11-libs/gtk+-2
			  >=gnome-base/gconf-2 )"
DEPEND="${RDEPEND}
	x11-proto/xproto
	app-arch/unzip"

S=${WORKDIR}/${MY_P}

enable_feature() {
	if use ${1} ; then
		sed -i \
			-e "/${2}.*=/s:#::" \
			makefile || die "sed makefile (${1} / ${2}) failed"
	fi
}

pkg_nofetch() {
	einfo "Please download ${SRC_URI}"
	einfo "and move it to ${DISTDIR}"
}

src_unpack() {
	unpack ${A}
	cd "${S}"

	sed -i \
		-e "/PM.*=/s:^:# :" \
		-e "/MIPS3_DRC.*= 1/s:^:# :" \
		-e "/PPC_DRC.*= 1/s:^:# :" \
		makefile || die "sed makefile failed"

	if use custom-cflags ; then
		sed -i \
			-e "s:-Wdeclaration-after-statement:-Wdeclaration-after-statement ${CFLAGS}:" \
			-e "s:LDFLAGS = -WO:LDFLAGS = ${LDFLAGS}:" \
			makefile || die "sed makefile custom-cflags failed"
	fi

	enable_feature amd64 AMD64
	enable_feature amd64 PTR64
	enable_feature ppc64 PTR64

	case $(get-flag march) in
		pentium3)	enable_feature x86 PM;;
		pentium-m)	enable_feature x86 PM;;
		pentium4)   enable_feature x86 P4;;
		athlon*)	enable_feature x86 ATHLON;;
		k7)			enable_feature x86 ATHLON;;
		g4)			enable_feature x86 G4;;
		g5)			enable_feature x86 G5;;
		i686)		enable_feature x86 I686;;
		pentiumpro)	enable_feature x86 I686;;
	esac

	use x86 && enable_feature dynarec X86_MIPS3_DRC
	use x86 && enable_feature dynarec X86_PPC_DRC
	use x86 && enable_feature dynarec X86_VOODOO_DRC

	enable_feature debug DEBUG
}

src_compile() {
	local target="all"
	
	emake \
		ARCH=$(get-flag -march) \
		CC=$(tc-getCC) \
		NAME=${PN} \
		${target} \
		|| die "emake failed"
}

src_install() {
	dogamesbin ${PN} || die "dogamesbin ${PN} failed"

	if use tools ; then
		exeinto "${GAMES_LIBDIR}/${PN}"
		local f
		for f in chdman file2str imgtool ; do
			doexe "${f}" || die "doexe ${f} failed"
		done
	fi

	dodoc docs/* *.txt

	prepgamesdirs
}
